//==================================================================MCC_fnc_IedDisablingExplosion===============================================================================================
// Create a disabling explosion, explosion dimiter will be decided by the _trapvolume
//Disabling explosion will disable vehicles without harming the troops inside or  it will incapitate infantry
// Example: [_pos,_trapvolume] spawn MCC_fnc_IedDisablingExplosion;
// _pos = position, center of the explosion.
// _trapvolume = string, "small", "medium", "large"
//==================================================================================================================================================================================

private ["_pos", "_volume","_hitRadius","_killRadius","_effected","_random","_shell","_effect","_burningEffects","_vel","_relVel"];
_pos 	= _this select 0;
_volume = _this select 1;
_random	= 0;

_burningEffects =
{
	private ["_object","_effect"];
	_object = _this;
	_effect = "test_EmptyObjectForFireBig" createVehicle (getpos _object);
	_effect attachto [_object,[0,0,0]];
	_effect spawn
	{
		sleep 180 + random 360;
		while {!isnull (attachedTo _this)} do {detach _this};
		_nearObjects =  (getpos _this) nearObjects 3;
		{
			if (typeOf _x in ["test_EmptyObjectForFireBig","#particlesource","#lightpoint"]) then {deletevehicle _x};
		} foreach _nearObjects;
	};
};

switch (_volume) do
{
   case "small":
	{
	   "SmallSecondary" createVehicle _pos;
	   _hitRadius 	= 20;
	   _killRadius	= 10;
	   _vel = 10;
	};

	case "medium":
	{
	   "M_AT" createVehicle _pos;
		_hitRadius = 30;
		_killRadius	= 20;
		_vel = 15;
	};

	case "large":
	{
	   "M_AT" createVehicle _pos;
	   _hitRadius = 50;
	   _killRadius	= 30;
	   _vel = 20;
	};
};

//ShockWave effect
_effected = (allPlayers inAreaArray [_pos, _hitRadius*2, _hitRadius*2, 0, false, _hitRadius]) select {vehicle _x == _x};
_effected = +_effected + (vehicles inAreaArray [_pos, _hitRadius*2, _hitRadius*2, 0, false, _hitRadius]);

{
	//Add val
	_relVel = (_vel * (1 - ((_pos distance2D vehicle _x)/_hitRadius))) max 1;
	[_x,_relVel,(_relVel/10),_pos] remoteExec ["MCC_fnc_addVelocity",_x];

	_random = random 10;
	if (_x isKindOf "Man") then
	{
		if (((_x distance _pos) < _killRadius) && (_random > 1))then
		{
			_x setHit ["legs", 0.9];
			_x setdamage 0.7;
		};
	};

	if(_x isKindOf "Car") then
	{
		if (((_x distance _pos) < _killRadius) && (_random > 1))then
		{
			_x setdamage 0.7;
			[_x, ["wheel_1_1_steering", 1]] remoteExec ["setHit", _x];
			[_x, ["wheel_2_1_steering", 1]] remoteExec ["setHit", _x];
			[_x, ["motor", 1]] remoteExec ["setHit", _x];
			[_x, ["glass1", 1]] remoteExec ["setHit", _x];
			[_x, ["glass2", 1]] remoteExec ["setHit", _x];
			[_x, ["glass3", 1]] remoteExec ["setHit", _x];
			[_x, ["glass4", 1]] remoteExec ["setHit", _x];
			[_x, ["glass5", 1]] remoteExec ["setHit", _x];
			[_x, ["glass6", 1]] remoteExec ["setHit", _x];
			if (isServer) then {_x spawn _burningEffects};
			_x spawn {
				sleep 15;
				if (!isNull _this) then {
					_this setdamage 1;
				};
			};
		}
		else
		{
			_x setdamage 0.4;
			[_x, ["wheel_1_1_steering", 1]] remoteExec ["setHit", _x];
			[_x, ["wheel_2_1_steering", 1]] remoteExec ["setHit", _x];
			[_x, ["motor", 0.7]] remoteExec ["setHit", _x];
			[_x, ["glass1", 1]] remoteExec ["setHit", _x];
			[_x, ["glass2", 1]] remoteExec ["setHit", _x];
			[_x, ["glass3", 1]] remoteExec ["setHit", _x];
			[_x, ["glass4", 1]] remoteExec ["setHit", _x];
			[_x, ["glass5", 1]] remoteExec ["setHit", _x];
			[_x, ["glass6", 1]] remoteExec ["setHit", _x];
		}
	};

	if(_x isKindOf "Tank") then
	{
		if (((_x distance _pos) < _killRadius) && (_random > 1))then
		{
			_x setdamage 0.7;
			[_x, ["Ltrack", 1]] remoteExec ["setHit", _x];
			[_x, ["Rtrack", 1]] remoteExec ["setHit", _x];
			[_x, ["motor", 1]] remoteExec ["setHit", _x];
			if (isServer) then {_x spawn _burningEffects};
			_x spawn {
				sleep 15;
				if (!isNull _this) then {
					_this setdamage 1;
				};
			};
		};
	};
} forEach _effected;

